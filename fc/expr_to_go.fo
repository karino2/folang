package main

import frt
import slice
import buf
import strings

// wrapper types.
package_info _ =
  let reinterpretEscape: string->string
  let uniqueTmpVarName: ()->string


let rgFVToGo  (toGo: Expr->string) (fvPair: NEPair) =
  let fn = fvPair.Name
  let fv = fvPair.Expr
  let fvGo = toGo fv
  $"{fn}: {fvGo}"


let rgToGo (toGo: Expr->string) (rg:RecordGen) =
  let rtype = rg.RecordType
  let b = buf.New ()
  frStructName FTypeToGo rtype |> buf.Write b
  buf.Write b "{"
  let fvGo = rg.FieldsNV
             |> slice.Map (rgFVToGo toGo)
             |> strings.Concat ", "
  buf.Write b fvGo
  buf.Write b "}"
  buf.String b

/*

  blockToGo implementation.

*/

/*
  []Stmt+Expr -> []GoStmt+ return Expr.
*/
let buildReturn (sToGo: Stmt->string) (eToGo: Expr->string) (reToGoRet: ReturnableExpr->string) (stmts:[]Stmt) (lastExpr: Expr) =
  let stmtGos = slice.Map sToGo stmts |> strings.Concat "\n"
  let lastGo = match lastExpr with
              | EReturnableExpr re -> reToGoRet re
              | _ ->
                let mayReturn = if ExprToType lastExpr = FUnit then "" else "return "
                let lg = eToGo lastExpr
                mayReturn + lg
  if stmtGos = "" then
    lastGo
  else
    stmtGos + "\n" + lastGo


let wrapFunc (toGo: FType->string) (rtype:FType) (goReturnBody:string) =
  let b = buf.New ()
  buf.Write b "(func () "
  toGo rtype |> buf.Write b
  buf.Write b " {\n"
  buf.Write b goReturnBody
  buf.Write b"})"
  buf.String b

let wrapFunCall (toGo: FType->string) (rtype:FType) (goReturnBody:string) =
  let wf = wrapFunc toGo rtype goReturnBody
  wf + "()"

let blockToGoReturn (sToGo: Stmt->string) (eToGo: Expr->string) (reToGoRet: ReturnableExpr->string) (block:Block) =
  buildReturn sToGo eToGo reToGoRet block.Stmts block.FinalExpr

let blockToGo (sToGo: Stmt->string) (eToGo: Expr->string) (reToGoRet: ReturnableExpr->string) (block:Block) =
  let goRet = blockToGoReturn sToGo eToGo reToGoRet block
  let rtype = ExprToType block.FinalExpr
  wrapFunCall FTypeToGo rtype goRet 

let lbToGo (bToRet: Block->string) (lb:LazyBlock) =
  let returnBody = bToRet lb.Block
  let rtype = ExprToType lb.Block.FinalExpr
  wrapFunc FTypeToGo rtype returnBody


/*

  MatchExprToGo implementation

*/

/*
Build following case header. (If unit or _ variable, generate code appropriately.)

  case IntOrBool_I:
    [VarName] := tmpV
*/
let umpToCaseHeader (uname: string) (ump: UnionMatchPattern) (tmpVarName: string) =
  let b = buf.New ()
  buf.Write b "case "
  unionCSName uname ump.CaseId |> buf.Write b
  buf.Write b ":\n"
  if ump.VarName <> "_" && ump.VarName <> "" then
    buf.Write b ump.VarName
    buf.Write b " := "
    buf.Write b tmpVarName
    buf.Write b ".Value"
    buf.Write b "\n"
  buf.String b

/*
 btogRet is bounded blockToGoReturn.

 Generate following codes.

case IntOrBool_I:
	[VarName] := tmpV
	body...

*/
let umrToCase (btogRet: Block->string) (uname: string) (tmpVarName: string) (umr: UnionMatchRule)  =
  let b = buf.New ()
  let mp = umr.UnionPattern
  umpToCaseHeader uname mp tmpVarName |> buf.Write b
  btogRet umr.Body |> buf.Write b
  buf.Write b "\n"
  buf.String b  

/*
default:
   body...

*/
let drToCase (btogRet: Block->string) (db: Block)  =
  let b = buf.New ()
  buf.Write b "default:\n"
  btogRet db |> buf.Write b
  buf.Write b "\n"
  buf.String b  

let umrHasNoCaseVar (umr: UnionMatchRule) =
  let pat = umr.UnionPattern
  pat.VarName = "" || pat.VarName = "_"

/*
  If atlease one value or not (if not, tmp var is not necessary).
*/
let umrHasCaseVar (rules:UnionMatchRules) =
  let allNoCaseF = slice.Forall umrHasNoCaseVar
  match rules with
  | UCaseOnly us -> not (allNoCaseF us)
  | UCaseWD uds -> not (allNoCaseF uds.Unions)


let umrToGoReturn (toGo: Expr->string) (btogRet: Block->string) target (rules:UnionMatchRules) =
  let ttype = ExprToType target
  let uttype = GoEval<UnionType> "CastNow[FType_FUnion](ttype).Value"
  let uname = utName uttype
  let hasCaseVar = umrHasCaseVar rules
  let tmpVarName = if hasCaseVar then uniqueTmpVarName () else ""
  let b = buf.New ()
  let umrstocases = slice.Map (umrToCase btogRet uname tmpVarName)
  let writeUmrs = fun umrs -> umrstocases umrs |> strings.Concat "" |> buf.Write b
  buf.Write b "switch "
  if hasCaseVar then
    buf.Write b tmpVarName
    buf.Write b " := "
  buf.Write b "("
  toGo target |> buf.Write b 
  buf.Write b ").(type){\n"
  // case code.
  match rules with
  | UCaseOnly us ->
    writeUmrs us
    buf.Write b "default:\npanic(\"Union pattern fail. Never reached here.\")\n"
  | UCaseWD uds ->
    writeUmrs uds.Unions
    drToCase btogRet uds.Default |> buf.Write b
  buf.Write b "}"
  buf.String b

/*
case "hogehoge":
	body...
*/
let smrToCase (btogRet: Block->string) (smr: StringMatchRule)  =
  let b = buf.New ()
  let lp = smr.LiteralPattern
  buf.Write b $`case "{lp}":`
  buf.Write b "\n"
  btogRet smr.Body |> buf.Write b
  buf.Write b "\n"
  buf.String b  


/*
var is already capture in switch.
So just emit following.

default:
   body...

*/
let svrToCase (btogRet: Block->string) (svr: StringVarMatchRule)  =
  let b = buf.New ()
  buf.Write b "default:\n"
  btogRet svr.Body |> buf.Write b
  buf.Write b "\n"
  buf.String b  

let smrToGoReturn (toGo: Expr->string) (btogRet: Block->string) target  (rules:StringMatchRules) =
  let b = buf.New ()
  let smrstocases = slice.Map (smrToCase btogRet)
  let writeSmrs = fun smrs -> smrstocases smrs |> strings.Concat "" |> buf.Write b
  match rules with
  | SCaseWV swv ->
    let vname = swv.VarRule.VarName
    buf.Write b $"switch {vname} :="
    buf.Write b "("
    toGo target |> buf.Write b 
    buf.Write b $"); {vname}"
    buf.Write b"{\n"
    writeSmrs swv.Literals
    svrToCase btogRet swv.VarRule |> buf.Write b
    buf.Write b "}"
  | SCaseWD sds ->
    buf.Write b "switch ("
    toGo target |> buf.Write b 
    buf.Write b "){\n"
    writeSmrs sds.Literals
    drToCase btogRet sds.Default |> buf.Write b
    buf.Write b "}"
  buf.String b


let meToGoReturn (toGo: Expr->string) (btogRet: Block->string) (me:MatchExpr) =
  match me.Rules with
  | RUnions ru -> umrToGoReturn toGo btogRet me.Target ru
  | RStrings su -> smrToGoReturn toGo btogRet me.Target su

let meToExpr (me:MatchExpr) =
  RMatchExpr me |> EReturnableExpr

let meToGo (toGo: Expr->string) (btogRet: Block->string) (me:MatchExpr) =
  let goret = meToGoReturn toGo btogRet me
  let rtype = ExprToType (meToExpr me)
  wrapFunCall FTypeToGo rtype goret

/*

  ReturnableExpr to Go implementation.

*/

let reToGoReturn (sToGo: Stmt->string) (eToGo: Expr->string) (rexpr:ReturnableExpr) : string=
  let rtgr = reToGoReturn sToGo eToGo
  let btogoRet = blockToGoReturn sToGo eToGo rtgr
  match rexpr with
  | RBlock b -> blockToGoReturn sToGo eToGo rtgr b
  | RMatchExpr me -> meToGoReturn eToGo btogoRet me

let reToGo (sToGo: Stmt->string) (eToGo: Expr->string) (rexpr:ReturnableExpr) : string=
  let rtgr = reToGoReturn sToGo eToGo
  let btogRet = blockToGoReturn sToGo eToGo rtgr
  match rexpr with
  | RBlock b -> blockToGo sToGo eToGo rtgr b
  | RMatchExpr me -> meToGo eToGo btogRet me

/*

FunCallToGo

*/


let ftiToParamName (i:int) (ft:FType) =
  $"_r{i}"

// ("_r0", FInt) -> "_r0 int"
let ntpairToParam (tGo:FType->string) (ntp: string*FType) =
  let tpgo = frt.Snd ntp |> tGo 
  let name = frt.Fst ntp
  $"{name} {tpgo}"

let varRefToGo (tGo:FType->string) (vr:VarRef) =
  match vr with
  | VRVar v -> v.Name
  | VRSVar sv ->
    sv.Var.Name + (tArgsToGo tGo sv.SpecList)

let fcPartialApplyGo (tGo:FType->string) (eGo:Expr->string) (fc:FunCall) =
  let funcType = fcToFuncType fc
  let fargTypes = fargs funcType
  let argNum = slice.Length fc.Args
  let restTypes = slice.Skip argNum fargTypes
  let restParamNames = slice.Mapi ftiToParamName restTypes
  let b = buf.New ()
  buf.Write b "(func ("
  slice.Zip restParamNames restTypes |> slice.Map (ntpairToParam tGo)
  |> strings.Concat ", " |> buf.Write b
  buf.Write b ") "
  let fret = freturn funcType
  if fret = FUnit then
    buf.Write b "{ "
  else
    tGo fret |> buf.Write b
    buf.Write b "{ return "
  varRefToGo tGo fc.TargetFunc |> buf.Write b
  buf.Write b "("
  slice.Map eGo fc.Args |> strings.Concat ", " |> buf.Write b
  buf.Write b ", "
  strings.Concat ", " restParamNames |> buf.Write b
  buf.Write b ") })"
  buf.String b

let fcUnitArgOnly (fc:FunCall) =
  let al = slice.Length fc.Args
  if al = 1 then
    EUnit = slice.Head fc.Args 
  else
    false        

let fcFullApplyGo (tGo:FType->string) (eGo:Expr->string) (fc:FunCall) =
  let b = buf.New ()
  varRefToGo tGo fc.TargetFunc |> buf.Write b
  buf.Write b "("
  if not (fcUnitArgOnly fc) then
    slice.Map eGo fc.Args |> strings.Concat ", " |> buf.Write b
  buf.Write b ")"
  buf.String b

let fcToGo (tGo:FType->string) (eGo:Expr->string) (fc:FunCall) =
  let funcType = fcToFuncType fc
  let fargTypes = fargs funcType
  let al = slice.Length fc.Args
  let tal = slice.Length fargTypes
  if al > tal then GoEval "panic(\"Too many argument\")"
  if al < tal then
    fcPartialApplyGo tGo eGo fc
  else
    fcFullApplyGo tGo eGo fc

let sliceToGo (tGo:FType->string) (eGo:Expr->string) (exprs: []Expr) =
  let b = buf.New ()
  buf.Write b "("
  ESlice exprs |> ExprToType |> tGo |> buf.Write b
  buf.Write b "{"
  slice.Map eGo exprs |> strings.Concat "," |> buf.Write b
  buf.Write b "}"
  buf.Write b ")"
  buf.String b

let tupleToGo (eGo:Expr->string) (exprs: []Expr) =
  let b = buf.New ()
  let len = slice.Length exprs
  buf.Write b $"frt.NewTuple{len}("
  slice.Map eGo exprs
  |> strings.Concat ", "
  |> buf.Write b
  buf.Write b ")"
  buf.String b

let binOpToGo (eGo:Expr->string) (binOp:BinOpCall) =
  let b = buf.New ()
  buf.Write b "("
  [binOp.Lhs; binOp.Rhs]
  |> slice.Map eGo
  |> strings.Concat binOp.Op
  |> buf.Write b
  buf.Write b ")"
  buf.String b

let faToGo (eGo:Expr->string) (fa:FieldAccess) =
  let target = eGo fa.TargetExpr
  $"{target}.{fa.FieldName}"

let paramsToGo (pm: Var) =
   let ts = FTypeToGo pm.Ftype
   $"{pm.Name} {ts}"

let lambdaToGo (bToGoRet: Block->string) (le:LambdaExpr) =
  let b = buf.New ()
  buf.Write b "func ("
  slice.Map paramsToGo le.Params
  |> strings.Concat ", "
  |> buf.Write b
  buf.Write b ")"
  blockToType ExprToType le.Body |> FTypeToGo |> buf.Write b
  buf.Write b "{\n"
  bToGoRet le.Body |> buf.Write b
  buf.Write b "\n}"
  buf.String b

package_info _ =
  let ParseSInterP: string->string*[]string

let sinterpToGo s =
  let (fm, vs) = ParseSInterP s
  let b = buf.New ()
  frt.Sprintf1 "frt.SInterP(\"%s\", " fm  |> buf.Write b
  vs |> strings.Concat ", " |> buf.Write b
  buf.Write b ")"
  buf.String b

  

let ExprToGo (sToGo: Stmt->string) (expr:Expr) : string =
  let eToGo = ExprToGo sToGo
  let reToGoRet = reToGoReturn sToGo eToGo 
  let bToGoRet = blockToGoReturn sToGo eToGo reToGoRet
  match expr with
  | EBoolLiteral b -> frt.Sprintf1 "%t" b
  | EGoEvalExpr ge -> reinterpretEscape ge.GoStmt
  | EStringLiteral s -> frt.Sprintf1 "\"%s\"" s
  | ESInterP sp -> sinterpToGo sp
  | EIntImm i -> frt.Sprintf1 "%d" i
  | EUnit -> ""
  | EFieldAccess fa -> faToGo eToGo fa
  | EVarRef vr ->varRefName vr
  | ESlice es -> sliceToGo FTypeToGo eToGo es
  | ETupleExpr es -> tupleToGo eToGo es
  | ELambda le -> lambdaToGo bToGoRet le
  | EBinOpCall bop -> binOpToGo eToGo bop
  | ERecordGen rg -> rgToGo eToGo rg
  | EReturnableExpr re -> reToGo sToGo eToGo re
  | EFunCall fc -> fcToGo FTypeToGo eToGo fc
  | ELazyBlock lb ->
    lbToGo bToGoRet lb

