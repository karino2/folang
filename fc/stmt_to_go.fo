package main
import frt
import buf
import slice
import strings

let imToGo (pn:string) =
  $"import \"{pn}\""

let pmToGo (pn:string) =
  $"package {pn}"

/*

  LetFuncDef to Go implementation.

*/


/*
  Emit parameters of func defs like following:
  a int, b string, c []int
*/
let lfdParamsToGo (lfd:LetFuncDef) =
   lfd.Params |> slice.Map paramsToGo |> strings.Concat ", "

let lfdToGo (bToGoRet: Block->string) (lfd:LetFuncDef) =
  let b = buf.New ()
  buf.Write b "func "
  buf.Write b lfd.Fvar.Name
  buf.Write b "("
  lfdParamsToGo lfd |> buf.Write b
  buf.Write b ") "
  blockToType ExprToType lfd.Body |> FTypeToGo |> buf.Write b
  buf.Write b "{\n"
  bToGoRet lfd.Body |> buf.Write b
  buf.Write b "\n}"
  buf.String b

// for poor tinyfo inference.
let pany (s:string) =
  frt.Sprintf1 "%s any" s

let writeTParamsIfAny b tparams =
  if not (slice.IsEmpty tparams) then
    buf.Write b "["
    tparams
    |> slice.Map pany
    |> strings.Concat ", "
    |> buf.Write b
    buf.Write b "]"

let rfdToGo (bToGoRet: Block->string) (rfd:RootFuncDef) =
  let lfd = rfd.Lfd
  let b = buf.New ()
  buf.Write b "func "
  buf.Write b lfd.Fvar.Name
  writeTParamsIfAny b rfd.Tparams
  buf.Write b "("
  lfdParamsToGo lfd |> buf.Write b
  buf.Write b ") "
  blockToType ExprToType lfd.Body |> FTypeToGo |> buf.Write b
  buf.Write b "{\n"
  bToGoRet lfd.Body |> buf.Write b
  buf.Write b "\n}"
  buf.String b


let lvdToGo (eToGo: Expr->string) (lvd:LetVarDef) =
  let rhs = eToGo lvd.Rhs
  lvd.Lvar.Name + " := " + rhs

let ldvdToGo (eToGo: Expr->string) (ldvd:LetDestVarDef) =
  let b = buf.New ()
  slice.Map _.Name ldvd.Lvars |> strings.Concat ", " |> buf.Write b
  buf.Write b " := frt.Destr("
  eToGo ldvd.Rhs |> buf.Write b
  buf.Write b ")"
  buf.String b

/*
  RecordDef to Go implementation.
*/

let rdffieldToGo (field: NameTypePair) =
  "  " + field.Name + " " + (FTypeToGo field.Ftype)

let rdfToGo (rdf:RecordDef) =
  let b = buf.New ()
  buf.Write b $"type {rdf.Name}"
  writeTParamsIfAny b rdf.Tparams
  buf.Write b " struct {\n"
  rdf.Fields |> slice.Map rdffieldToGo |> strings.Concat "\n" |> buf.Write b
  buf.Write b "\n}" 
  buf.String b

/*
   UnionDef to Go implementation.
*/

/*
Union implementation.
For following code:
type IntOrString =

	| I of int
	| B of bool

The result becomes following three types.

- IntOrString interface
- IntOrString_I struct (with Value int)
- IntOrString_B struct (with Value bool)

We call IntOrString_I "case struct of I".
*/

/*
	type IntOrString interface {
	  IntOrString_Union()
	}
*/
let udUnionDef (ud:UnionDef) =
  let b = buf.New ()
  buf.Write b $"type {ud.Name}"
  writeTParamsIfAny b ud.Tparams
  buf.Write b " interface {\n"
  buf.Write b $"  {ud.Name}_Union()\n"
  buf.Write b "}\n"
  buf.String b


/*
  If tparams is empty, return ""
  If not, it will return somthing like:
  "[T0, T1]"
  no "any".
*/
let toStringTParamsIfAny tparams =
  if slice.IsEmpty tparams then
    ""
  else
    strings.Concat ", " tparams
    |> strings.EncloseWith "[" "]"


/*
func (IntOrString_I) [method]

If there is type parameter, it becomes:

func (Result_Success[T]) [method]
*/
let csToConformMethod (uname:string) tparams (method:string) (cas: NameTypePair) =
  let csname = unionCSName uname cas.Name
  let tparaStr = toStringTParamsIfAny tparams
  $"func ({csname}{tparaStr}) {method}"

/*
func (IntOrString_I) IntOrString_Union(){}
func (IntOrString_B) IntOrString_Union(){}
*/
let udCSConformMethods (ud:UnionDef) =
  let method =  $"{ud.Name}_Union()" + "{}\n"
  udCases ud |> slice.Map (csToConformMethod ud.Name ud.Tparams method) |> strings.Concat ""

/*
	type IntOrString_I struct {
	   Value int
	}

  If there ara Tparams, it becomes:

  type Result_Success[T any] struct {
    Value T
  }
*/
let udCSDef (ud:UnionDef) (cas: NameTypePair) =
  let b = buf.New ()
  buf.Write b "type "
  unionCSName ud.Name cas.Name |> buf.Write b
  writeTParamsIfAny b ud.Tparams
  buf.Write b " struct {\n"
  if cas.Ftype <> FUnit then
    buf.Write b "  Value "
    FTypeToGo cas.Ftype |> buf.Write b
    buf.Write b "\n"
  buf.Write b "}\n"
  buf.String b

// New_IntOrString_I
let csConstructorName  (unionName: string) (cas: NameTypePair) =
   "New_" + (unionCSName unionName cas.Name)

/*
CS Constructor, function case.

func New_IntOrString_I(v int) IntOrString { return IntOrString_I{v} }

If Tparams, it might becomes:

func New_Result_Success[T any](v T) Result[T] { return Result_Success[T]{v} }
*/
let csConstructFunc (uname:string) tparams (cas: NameTypePair) =
  let b = buf.New ()
  buf.Write b "func "
  csConstructorName uname cas |> buf.Write b
  writeTParamsIfAny b tparams  
  buf.Write b "("
  if cas.Ftype <> FUnit then
    buf.Write b "v "
    FTypeToGo cas.Ftype |> buf.Write b
  buf.Write b ") "
  buf.Write b uname
  // "[T0, T1]" if any, else ""
  let targ = toStringTParamsIfAny tparams
  buf.Write b targ
  buf.Write b " { return "
  buf.Write b (unionCSName uname cas.Name)
  buf.Write b targ
  buf.Write b "{"
  if cas.Ftype <> FUnit then
    buf.Write b "v"
  buf.Write b "} }\n"
  buf.String b


/*
CS Constructor, variable case.

No arg case constructor case.
In this case, folang regard no arg func as variable.
So the result must be following:

var New_IntOrString_I IntOrString = &IntOrString_I{}
*/
let csConstructVar (uname:string) (cas: NameTypePair) =
  let b = buf.New ()
  buf.Write b "var "
  csConstructorName uname cas |> buf.Write b
  buf.Write b $" {uname} = "
  buf.Write b (unionCSName uname cas.Name)
  buf.Write b "{}\n"
  buf.String b

let csIsVar tparams (cas: NameTypePair) =
  cas.Ftype = FUnit && (slice.IsEmpty tparams)

let csConstruct (uname:string) tparams (cas: NameTypePair) =
  if (csIsVar tparams cas) then
    csConstructVar uname cas
  else
    csConstructFunc uname tparams cas

let caseToGo (ud:UnionDef) (cas: NameTypePair) =
  let sdf = udCSDef ud cas
  let csdf = csConstruct ud.Name ud.Tparams cas
  sdf + "\n" + csdf + "\n"

let udfToGo (ud:UnionDef) =
  let b = buf.New ()
  udUnionDef ud |> buf.Write b
  buf.Write b "\n"
  udCSConformMethods ud |> buf.Write b
  buf.Write b "\n"
  udCases ud |> slice.Map (caseToGo ud) |> strings.Concat "" |> buf.Write b
  buf.String b

/*
  DefStmt to Go, MultpleDefs to Go
*/

let dsToGo (ds:DefStmt) =
   match ds with
   | DRecordDef rd -> rdfToGo rd
   | DUnionDef ud -> udfToGo ud

let mdToGo (md:MultipleDefs) =
  md.Defs |>  slice.Map dsToGo |> strings.Concat "\n"

let StmtToGo (stmt:Stmt) : string=
  let eToGo = ExprToGo StmtToGo
  match stmt with
  | SLetVarDef llvd ->
    match llvd with
    | LLOneVarDef lvd -> lvdToGo eToGo lvd
    | LLDestVarDef ldvd -> ldvdToGo eToGo ldvd
  | SExprStmt expr -> eToGo expr

let rootVarDefToGo eToGo (rvd: RootVarDef) =
  let vname = rvd.Vdef.Lvar.Name
  let rhs = rvd.Vdef.Rhs
  let b = buf.New ()
  buf.Write b $"var {vname} = "
  eToGo rhs |> buf.Write b
  buf.String b

let RootStmtToGo (rstmt:RootStmt) : string=
  let eToGo = ExprToGo StmtToGo
  let reToGoRet = reToGoReturn StmtToGo eToGo
  let bToGoRet = blockToGoReturn StmtToGo eToGo reToGoRet
  match rstmt with
  | RSImport im -> imToGo im
  | RSPackage pn -> pmToGo pn
  | RSPackageInfo -> ""
  | RSRootFuncDef rfd -> rfdToGo bToGoRet rfd
  | RSRootVarDef rvd -> rootVarDefToGo eToGo rvd
  | RSDefStmt ds -> dsToGo ds
  | RSMultipleDefs md -> mdToGo md



let RootStmtsToGo (rstmts:[]RootStmt) =
  slice.Map RootStmtToGo rstmts |> strings.Concat "\n\n" |> strings.AppendTail "\n"
